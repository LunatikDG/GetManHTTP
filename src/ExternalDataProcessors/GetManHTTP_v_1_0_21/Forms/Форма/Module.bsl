
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	СохранитьНастройкиОбработки();
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ВосстановитьНастройкиОбработки();

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипАвторизацииПриИзменении(Элемент)
	
	СкрытьГруппыАвторизаций();
	
	Если ТипАвторизации = "BasicAuth" Тогда
		Элементы.ЛогинПароль.Видимость = Истина;
	ИначеЕсли ТипАвторизации = "Token" Тогда
		Элементы.ГруппаТокен.Видимость = Истина;
	Иначе
		Элементы.ЛогинПароль.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТелоЗапросаПриИзменении(Элемент)
	БьютифицированныйЗапрос(ТелоЗапроса, "ТелоЗапроса");
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	ПроверкаУсловий = УсловияОтправкиВыполнены();
	
	Если ПроверкаУсловий.УсловияВыполнены  Тогда
		ОтправитьHTTPЗапросАсинх(СтруктураURI(URI));
	Иначе
		//@skip-check use-non-recommended-method
		Сообщить("Запрос не может быть отправлен, поскольку не все обязательные поля были заполнены");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// КЛИЕНТ

// Приводит значения реквизитов в формы к изначальному виду
//
&НаКлиенте
Процедура ЗначенияПоУмолчанию()
	
	ЗапросОтвет = "";
	КодОтвета = 0;
	
КонецПроцедуры

// Записывает ответ в бьютифицрованном виде (красивом)
//
// Параметры:
//	СтрокаОтвета 	- Строка - Строка формата JSON для форматирования
//	Поле			- Строка - Наименование поле формы которые необходимо преобразовать
//
&НаКлиенте
Процедура БьютифицированныйЗапрос(Знач СтрокаОтвета, Знач Поле)

	Если ПустаяСтрока(СтрокаОтвета) ИЛИ ПустаяСтрока(Поле) Тогда
		Возврат;
	КонецЕсли;
	
	#Если НЕ ВебКлиент Тогда
		
		// Читаем строку как JSON
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(СтрокаОтвета);
		Данные = ПрочитатьJSON(Чтение);
		
		// Производим бьютификацию по параметрам с символами табуляции
		Запись = Новый ЗаписьJSON();
		ПараметрыJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
		Запись.УстановитьСтроку(ПараметрыJSON);
		ЗаписатьJSON(Запись, Данные);
		
		ЭтотОбъект[Поле] = Запись.Закрыть();
		
	#Иначе
		ЭтотОбъект[Поле] = СтрокаОтвета;
	#КонецЕсли
	
	

КонецПроцедуры

// Скрывает группы авторизаций на форме
//
&НаКлиенте
Процедура СкрытьГруппыАвторизаций()

	Элементы.ЛогинПароль.Видимость = Ложь;
	Элементы.ГруппаТокен.Видимость = Ложь;

КонецПроцедуры

// Сформировать заголовки запроса
// 
// Возвращаемое значение:
//	Произвольный - заголовки HTTP-запроса
//
&НаКлиенте
Функция ЗаголовкиЗапроса()

	ЗаголовкиЗапросаПользователя = ЗаголовкиВСоответствие();
	
	// Дополняем заголовки авторизацией
	Если ТипАвторизации = "Token" Тогда
		СтрокаТокена = ?(ПустаяСтрока(НаименованиеТокена), "Token ", НаименованиеТокена + " ") + ТокенАвторизации;
		ЗаголовкиЗапросаПользователя.Вставить("Authorization", СтрокаТокена);
	КонецЕсли;

	Возврат ЗаголовкиЗапросаПользователя;

КонецФункции

// Проверяет выполнение всех условий для отправки HTTP-запроса
//
// Возвращаемое значение:
//	Структура:
//		* УсловияВыполнены - Булево - Признак того может ли быть отправлен HTTP-запроса
//
&НаКлиенте
Функция УсловияОтправкиВыполнены()
	Результат = Новый Структура;
	
	Результат.Вставить("УсловияВыполнены", Ложь);
	
	
	// Проверяем поля
	Если НЕ ПустаяСтрока(URI) И НЕ ПустаяСтрока(HTTPМетод) Тогда
		Результат.УсловияВыполнены = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Разбирает строку URI на составные части и возвращает в виде структуры.
// На основе RFC 3986.
//
// Параметры:
//  СтрокаURI - Строка - ссылка на ресурс в формате:
//                       <схема>://<логин>:<пароль>@<хост>:<порт>/<путь>?<параметры>#<якорь>.
//
// Возвращаемое значение:
//  Структура - составные части URI согласно формату:
//   * Схема        	- Строка - схема из URI.
//   * Логин         	- Строка - логин из URI.
//   * Пароль        	- Строка - пароль из URI.
//   * ИмяСервера    	- Строка - часть <хост>:<порт> из URI.
//   * Хост          	- Строка - хост из URI.
//   * Порт          	- Число, Неопределено - порт из URI.
//   * ПутьНаСервере 	- Строка - часть <путь>?<параметры>#<якорь> из URI.
//   * ТипАвторизации 	- Строка - Используемый тип авторизации
//
&НаКлиенте
Функция СтруктураURI(Знач СтрокаURI) Экспорт
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема
	Схема = "";
	Позиция = СтрНайти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;
	
	// Строка соединения и путь на сервере.
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = СтрНайти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
	
	// Информация пользователя и имя сервера.
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = СтрНайти(СтрокаСоединения, "@");
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = СтрНайти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = "";
	Позиция = СтрНайти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = Сред(ИмяСервера, Позиция + 1);
		Если Не ТолькоЦифрыВСтроке(Порт) Тогда
			Порт = "";
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", ?(ПустаяСтрока(Порт), Неопределено, Число(Порт)));
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	Результат.Вставить("ТипАвторизации", "");
	
	ДополнитьАвторизациюВСтруктурURI(Результат);
	
	ДополнитьПараметрыЗапросаВСтруктуруURI(Результат);

	Возврат Результат;
	
	
КонецФункции

// Проверяет, содержит ли строка только цифры.
//
// Параметры:
//  Значение         - Строка - проверяемая строка.
//  Устаревший       - Булево - устаревший параметр, не используется.
//  ПробелыЗапрещены - Булево - если Ложь, то в строке допустимо наличие пробелов.
//
// Возвращаемое значение:
//   Булево - Истина - строка содержит только цифры или пустая, Ложь - строка содержит иные символы.
//
// Пример:
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("0123"); // Истина
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("0123abc"); // Ложь
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("01 2 3",, Ложь); // Истина
//
&НаКлиенте
Функция ТолькоЦифрыВСтроке(Знач Значение, Знач Устаревший = Истина, Знач ПробелыЗапрещены = Истина) Экспорт
	
	Если ТипЗнч(Значение) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПробелыЗапрещены Тогда
		Значение = СтрЗаменить(Значение, " ", "");
	КонецЕсли;
		
	Если СтрДлина(Значение) = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Если содержит только цифры, то в результате замен должна быть получена пустая строка.
	// Проверять при помощи ПустаяСтрока нельзя, так как в исходной строке могут быть пробельные символы.
	Возврат СтрДлина(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( 
			Значение, "0", ""), "1", ""), "2", ""), "3", ""), "4", ""), "5", ""), "6", ""), "7", ""), "8", ""), "9", "")) = 0;
	
КонецФункции

// Дополняет структуру URI авторизацией (Basic Auth)
// 
// Параметры:
//	URI - Структура:
//			- см. СтруктураURI
&НаКлиенте
Процедура ДополнитьАвторизациюВСтруктурURI(URI)

	URI.ТипАвторизации = ТипАвторизации;
	
	Если ТипАвторизации = "BasicAuth" Тогда
		URI.Логин = ЛогинBasicAuth;
		URI.Пароль = ПарольBasicAuth;
	КонецЕсли;

КонецПроцедуры

// Дополнить структуру URI параметрами запроса
// Параметры:
//	URI - Структура:
//			- см. СтруктураURI
&НаКлиенте
Процедура ДополнитьПараметрыЗапросаВСтруктуруURI(URI)

	URI.ПутьНаСервере = URI.ПутьНаСервере + ПараметрыВСтроку();

КонецПроцедуры

// Отправка HTTP-запроса по указанным данным пользователем
// Параметры:
//	URI - Структура:
//			- см. СтруктураURI
//
&НаКлиенте
Асинх Процедура ОтправитьHTTPЗапросАсинх(URI)
	
	ЗначенияПоУмолчанию();
	ТекстОтвета = "";
	
	Если URI.Схема = "https" Тогда
		Если URI.ТипАвторизации = "BasicAuth" Тогда
			HTTPСоединение = Новый HTTPСоединение(URI.Хост, 443, URI.Логин, URI.Пароль ,Новый ЗащищенноеСоединениеOpenSSL());
		Иначе
			HTTPСоединение = Новый HTTPСоединение(URI.Хост, 80 );
		КонецЕсли;
	Иначе
		Если URI.ТипАвторизации = "BasicAuth" Тогда
			HTTPСоединение = Новый HTTPСоединение(URI.Хост, 80, URI.Логин, URI.Пароль);
		Иначе
			HTTPСоединение = Новый HTTPСоединение(URI.Хост, 80);
		КонецЕсли;
	КонецЕсли;
	
	HTTPЗапрос = Новый HTTPЗапрос(URI.ПутьНаСервере);
	
	Если ЗначениеЗаполнено(ТелоЗапроса)Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
	КонецЕсли;
	
	HTTPЗапрос.Заголовки = ЗаголовкиЗапроса();
	
	СтартЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Обещание  = HTTPСоединение.ВызватьHTTPМетодАсинх(HTTPМетод, HTTPЗапрос);
	
	Результат = Ждать Обещание;
	
	КонецЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	ВремяВыполненияЗапроса = Строка(КонецЗамера - СтартЗамера) + " ms";
	
	КодОтвета = Результат.КодСостояния;
	
	ЗаполнитьЗаголовкиОтвета(Результат.Заголовки);
	
	Если Результат.КодСостояния = 200 Тогда
		ТекстОтвета = Результат.ПолучитьТелоКакСтроку()
	Иначе
		ТекстОтвета = Результат.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	БьютифицированныйЗапрос(ТекстОтвета, "ЗапросОтвет");
	
КонецПроцедуры

// СЕРВЕР

// Процедура заполняет реквизит формы заголовков из соответствия
//
// Параметры:
//	Соответствие - Произвольный - Заголовки результата запроса
//
&НаСервере
Процедура ЗаполнитьЗаголовкиОтвета(Знач Соответствие)

	ЗаголовкиОтвета.Загрузить(СоответствиеВЗаголовки(Соответствие));

КонецПроцедуры

// Преобразует таблицу значений в массив структур.
// Может использоваться для передачи на клиент данных в том случае, если таблица
// значений содержит только такие значения, которые могут
// быть переданы с сервера на клиент.
//
// Полученный массив содержит структуры, каждая из которых повторяет
// структуру колонок таблицы значений.
//
// Не рекомендуется использовать для преобразования таблиц значений
// с большим количеством строк.
//
// Возвращаемое значение:
//  Произвольный - коллекция строк таблицы в виде структур.
//
&НаСервере
Функция ЗаголовкиВСоответствие()
    // Создаем пустое соответствие
    Соответствие = Новый Соответствие;
    ЗаголовкиЗапросаТЗ = ЗаголовкиЗапроса.Выгрузить();
    // Проверка наличия строк в таблице
    Если ЗаголовкиЗапросаТЗ.Количество() = 0 Тогда
        Возврат Соответствие; // Возвращаем пустое соответствие
    КонецЕсли;
    
    // Проходим по всем строкам таблицы значений
    Для Каждого Строка Из ЗаголовкиЗапросаТЗ Цикл
        // Проверяем наличие хотя бы двух столбцов
        Если ЗаголовкиЗапросаТЗ.Колонки.Количество() >= 2 Тогда
            Ключ = Строка[0];
            Значение = Строка[1];
            // Добавляем в соответствие
            Соответствие.Вставить(Ключ, Значение);
        КонецЕсли;
    КонецЦикла;
    
    // Возвращаем готовое соответствие
    Возврат Соответствие;
КонецФункции

// Преобразует таблицу значений в массив структур.
// Может использоваться для передачи на клиент данных в том случае, если таблица
// значений содержит только такие значения, которые могут
// быть переданы с сервера на клиент.
//
// Полученный массив содержит структуры, каждая из которых повторяет
// структуру колонок таблицы значений.
//
// Не рекомендуется использовать для преобразования таблиц значений
// с большим количеством строк.
//
// Параметры:
//  Соответствие - Соответствие - коллекция строк таблицы в виде структур.
//  
// Возвращаемое значение:
// 	ТаблицаЗначений:
// 		* КлючЗаголовка - Строка - Ключ соответствия
// 		* ЗначениеЗаголовка - Строка - Значение соответствия
// 		* ОписаниеЗаголовка - Строка - Описание ключа соответствия
//
&НаСервере
Функция СоответствиеВЗаголовки(Знач Соответствие)
	
	ТЗ = Новый ТаблицаЗначений();
	ТЗ.Колонки.Добавить("КлючЗаголовка");
	ТЗ.Колонки.Добавить("ЗначениеЗаголовка");
	ТЗ.Колонки.Добавить("ОписаниеЗаголовка");
	
    // Проходим по всем строкам таблицы значений
    Для Каждого Элемент Из Соответствие Цикл
    	
    	НоваяСтрока = ТЗ.Добавить();
    	
    	НоваяСтрока.КлючЗаголовка = Элемент.Ключ;
    	НоваяСтрока.ЗначениеЗаголовка = Элемент.Значение;
    	
    КонецЦикла;
    
    Возврат ТЗ;
    
КонецФункции

&НаСервере
Функция ПараметрыВСтроку()
	
	СтрокаПараметров = "";
	
	ТаблицаПараметров = ПараметрыЗапроса.Выгрузить();
	
	Для Каждого Параметр ИЗ ТаблицаПараметров Цикл
	
		СтрокаПараметров = СтрокаПараметров + СтрШаблон("%1=%2&", Параметр[0], Параметр[1]);
	
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(СтрокаПараметров) Тогда
		СтрокаПараметров = "?" + Лев(СтрокаПараметров, СтрДлина(СтрокаПараметров)-1);
	КонецЕсли;
	
	Возврат СтрокаПараметров;
	
КонецФункции

&НаСервере
Функция ПолучитьИмяОбработки()
	
	Возврат РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
	
КонецФункции 


#Область НастройкиОбработки

&НаСервере
Процедура СохранитьНастройкиОбработки()

	Настройки = Новый Соответствие();
	
	Настройки.Вставить("HTTPМетод", HTTPМетод);
	Настройки.Вставить("URI", URI);
	Настройки.Вставить("ТелоЗапроса", ТелоЗапроса);
	Настройки.Вставить("ВремяВыполненияЗапроса", ВремяВыполненияЗапроса);
	Настройки.Вставить("ЗаголовкиЗапроса", ЗначениеВСтрокуВнутр(ЗаголовкиЗапроса.Выгрузить()));
	Настройки.Вставить("ЗаголовкиОтвета", ЗначениеВСтрокуВнутр(ЗаголовкиОтвета.Выгрузить()));
	Настройки.Вставить("ЗапросОтвет", ЗапросОтвет);
	Настройки.Вставить("КодОтвета", КодОтвета);
	Настройки.Вставить("ЛогинBasicAuth", ЛогинBasicAuth);
	Настройки.Вставить("НаименованиеТокена", НаименованиеТокена);
	Настройки.Вставить("ПараметрыЗапроса", ЗначениеВСтрокуВнутр(ПараметрыЗапроса.Выгрузить()));
	Настройки.Вставить("ПарольBasicAuth", ПарольBasicAuth);
	Настройки.Вставить("ТипАвторизации", ТипАвторизации);
	Настройки.Вставить("ТокенАвторизации", ТокенАвторизации);
	
	ХранилищеСистемныхНастроек.Сохранить(ПолучитьИмяОбработки() + ".Форма.Форма/ТекущиеДанные", , Настройки);

КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиОбработки()

	НастройкиФормы = ХранилищеСистемныхНастроек.Загрузить(ПолучитьИмяОбработки() + ".Форма.Форма/ТекущиеДанные");
	
	Если НастройкиФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	HTTPМетод = НастройкиФормы.Получить("HTTPМетод");
	URI = НастройкиФормы.Получить("URI");
	ТелоЗапроса = НастройкиФормы.Получить("ТелоЗапроса");
	ВремяВыполненияЗапроса = НастройкиФормы.Получить("ВремяВыполненияЗапроса");
	// Заголовки запроса
	ЗаголовкиЗапроса_ХР = НастройкиФормы.Получить("ЗаголовкиЗапроса");
	Если ЗаголовкиЗапроса_ХР <> Неопределено Тогда
		ЗаголовкиЗапроса.Загрузить(ЗначениеИзСтрокиВнутр(ЗаголовкиЗапроса_ХР));
	КонецЕсли;
	// Заголовки ответа
	ЗаголовкиОтвета_ХР = НастройкиФормы.Получить("ЗаголовкиОтвета");
	Если ЗаголовкиОтвета_ХР <> Неопределено Тогда
		ЗаголовкиОтвета.Загрузить(ЗначениеИзСтрокиВнутр(ЗаголовкиОтвета_ХР));
	КонецЕсли;
	ЗапросОтвет = НастройкиФормы.Получить("ЗапросОтвет");
	КодОтвета = НастройкиФормы.Получить("КодОтвета");
	ЛогинBasicAuth = НастройкиФормы.Получить("ЛогинBasicAuth");
	НаименованиеТокена = НастройкиФормы.Получить("НаименованиеТокена");
	// Параметры запроса
	ПараметрыЗапроса_ХР = НастройкиФормы.Получить("ПараметрыЗапроса");
	Если ПараметрыЗапроса_ХР <> Неопределено Тогда
		ПараметрыЗапроса.Загрузить(ЗначениеИзСтрокиВнутр(ПараметрыЗапроса_ХР));
	КонецЕсли;
	ПарольBasicAuth = НастройкиФормы.Получить("ПарольBasicAuth");
	ТипАвторизации = НастройкиФормы.Получить("ТипАвторизации");
	ТокенАвторизации = НастройкиФормы.Получить("ТокенАвторизации");
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти
